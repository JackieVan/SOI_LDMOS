# 工艺仿真
go athena
# 定义网格x轴和y轴
line x loc=0.00 spac=0.4
line x loc=0.5 spac=0.1
line x loc=0.6 spac=0.1
line x loc=1 spac=0.08
line x loc=2 spac=0.3
line x loc=3 spac=0.5
line x loc=5 spac=0.05
line x loc=6 spac=0.3
line x loc=7 spac=0.1
line x loc=7.4 spac=0.1
line x loc=7.5 spac=0.1
line x loc=8 spac=0.4
#
line y loc=0.00 spac=0.01
line y loc=0.2 spac=0.015
line y loc=0.5 spac=0.06
line y loc=1 spac=0.12
line y loc=2.0 spac=2
# 定义衬底材料，类型，晶向
init silicon boron resistivity=1000 orientation=100
# 刻蚀硅
etch silicon above p1.y=1.16
# 生长二氧化硅层
diffus time=30 temp=1000 dryo2
# 
diffus time=30 temp=1000 weto2
# 
diffus time=30 temp=1000 dryo2
# 淀积硅
deposit silicon thick=1 c.boron=1.0e15
# 生长栅氧层
diffus time=30 temp=1000 dryo2
# 
etch oxide all
# 
method grid.ox=0.01
diffus time=50 temp=1000 dryo2 press=1.00 hcl.pc=3
# 
extract name="gateox" thickness material="SiO~2" mat.occno=1 x.val=-10
# 离子注入
implant boron dose=6e11 energy=20 pearson

# 在完成栅氧制备和离子注入后，对栅氧进行刻蚀
etch oxide start x=4.0 y=-1
etch cont x=4.0 y=0.2
etch cont x=4.5 y=0.2
etch done x=4.5 y=-1
# 刻蚀硅薄膜
etch silicon start x=4.0 y=-1
etch cont x=4.0 y=0.2
etch cont x=4.5 y=0.2
etch done x=4.5 y=-1

etch oxide start x=5.0 y=-1
etch cont x=5.0 y=0.2
etch cont x=5.5 y=0.2
etch done x=5.5 y=-1
# 刻蚀硅薄膜
etch silicon start x=5.0 y=-1
etch cont x=5.0 y=0.2
etch cont x=5.5 y=0.2
etch done x=5.5 y=-1

deposit oxide thick=0.28

etch oxide start x=0 y=-1
etch cont x=0 y=-0.03
etch cont x=1 y=-0.03
etch done x=1 y=-1

etch oxide start x=1 y=-1
etch cont x=1 y=-0.03
etch cont x=3.5 y=-0.03
etch done x=3.5 y=-1

etch oxide start x=3.5 y=-1
etch cont x=3.5 y=-0.03
etch cont x=8 y=-0.03
etch done x=8 y=-1
struct outfile=temp.str


# 多晶硅淀积
deposit polysilicon thick=0.35 divisions=10
# 刻蚀多晶硅栅
etch polysilicon left p1.x=1
etch polysilicon right p1.x=3.5
# 磷注入
implant phosphor dose=2e12 energy=100 pearson
# 源漏极注入
deposit barrier thick=0.01 div=1
etch barrier left p1.x=2
etch barrier right p1.x=7
implant phos dose=3.0e15 energy=100 pearson
strip
# 退火
method fermi compress
impurity i.phosph poly /oxide trn.0=0.0
diffus time=30 temp=1000 nitro press=1.00
# 刻蚀氧化层
etch oxide left p1.x=0.5
etch oxide right p1.x=7.5
# 淀积接触金属
deposit aluminum thick=0.1 divisions=2
etch aluminum start x=0.6 y=-10
etch cont x=0.6 y=10
etch cont x=7.4 y=10
etch done x=7.4 y=-10
# 定义淀积
electrode name=source x=0.3
electrode name=gate x=2 y=-0.2
electrode name=drain x=7.7
electrode name=substrate backside
# 
struct outfile=ldmos.str

# 器件仿真之阈值电压和输出特性曲线
go atlas
#
mesh  infile=ldmos.str
#
models cvt srh print 
impact selb
#
contact name=gate n.poly 
#
interface qf=3e10
#
method    newton trap
solve init
solve vdrain=0.1 
#
log outf=ldmos1_0.log
solve name=gate vgate=0 vfinal=3.3 vstep=0.1
tonyplot ldmos1_0.log
extract name="vt"
 (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain"))))\
- abs(ave(v."drain"))/2.0)
extract name="beta" 
slope(maxslope(curve(abs(v."gate"),abs(i."drain")))) * \
 (1.0/abs(ave(v."drain")))
extract name="theta"
 ((max(abs(v."drain")) * $"beta")/max(abs(i."drain"))) - \
 (1.0 / (max(abs(v."gate")) - ($"vt")))
log off
solve vgate=1.5 outf=solve1
solve vgate=2.2 outf=solve2
solve vgate=3.3 outf=solve3
solve vgate=4.4 outf=solve4
#
load   infile=solve1
log outf=ldmos2_0.log
solve name=drain vdrain=0 vfinal=8 vstep=0.5
#
load   infile=solve2
log outf=ldmos3_0.log
solve name=drain vdrain=0 vfinal=8 vstep=0.5
#
load   infile=solve3
log outf=ldmos4_0.log
solve name=drain vdrain=0 vfinal=8 vstep=0.5
#
load   infile=solve4
log outf=ldmos5_0.log
solve name=drain vdrain=0 vfinal=8 vstep=0.5
tonyplot -overlay ldmos2_0.log ldmos3_0.log ldmos4_0.log ldmos5_0.log


#器件仿真之击穿电压
go atlas
mesh infile=ldmos.str
contact name=gate n.polysilicon
interf qf=3e10
models print cvt consrh
impact selb
method newton trap climit=1e-4
log outf=ldmos_jc.log
solve vdrain=0.025
solve vdrain=0.05
solve vdrain=0.1
solve vdrain=0.5
solve vstep=0.25 vfinal=16 name=drain compl=5e-9 cname=drain
# Extract the design parameter, Vbd
extract name="NVbd"
 x.val from curve(abs(v."drain"),abs(i."drain")) where y.val=1e-9
tonyplot  ldmos_jc.log
quit


# 器件仿真之导通电阻
go atlas
mesh infile=ldmos.str
models cvt srh print
impact selb
contact name=gate n.poly
interface qf=3e10
method newton trap
solve init
solve vgate=3
log outf=ldmos6_0.log
solve name=drain vdrain=0 vfinal=0.1 vstep=0.005
tonyplot ldmos6_0.log
